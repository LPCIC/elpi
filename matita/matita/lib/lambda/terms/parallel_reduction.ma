(**************************************************************************)
(*       ___                                                              *)
(*      ||M||                                                             *)
(*      ||A||       A project by Andrea Asperti                           *)
(*      ||T||                                                             *)
(*      ||I||       Developers:                                           *)
(*      ||T||         The HELM team.                                      *)
(*      ||A||         http://helm.cs.unibo.it                             *)
(*      \   /                                                             *)
(*       \ /        This file is distributed under the terms of the       *)
(*        v         GNU General Public License Version 2                  *)
(*                                                                        *)
(**************************************************************************)

include "lambda/terms/size.ma".
include "lambda/terms/sequential_reduction.ma".

(* PARALLEL REDUCTION (SINGLE STEP) *****************************************)

(* Note: the application "(A B)" is represented by "@B.A"
         as for sequential reduction
*)
inductive pred: relation term â‰
| pred_vref: âˆ€i. pred (#i) (#i)
| pred_abst: âˆ€A1,A2. pred A1 A2 â†’ pred (ğ›Œ.A1) (ğ›Œ.A2) 
| pred_appl: âˆ€B1,B2,A1,A2. pred B1 B2 â†’ pred A1 A2 â†’ pred (@B1.A1) (@B2.A2)
| pred_beta: âˆ€B1,B2,A1,A2. pred B1 B2 â†’ pred A1 A2 â†’ pred (@B1.ğ›Œ.A1) ([â†™B2]A2)
.

interpretation "parallel reduction"
    'ParRed M N = (pred M N).

lemma pred_refl: reflexive â€¦ pred.
#M elim M -M // /2 width=1/
qed.

lemma pred_inv_vref: âˆ€M,N. M â¤‡ N â†’ âˆ€i. #i = M â†’ #i = N.
#M #N * -M -N //
[ #A1 #A2 #_ #i #H destruct
| #B1 #B2 #A1 #A2 #_ #_ #i #H destruct
| #B1 #B2 #A1 #A2 #_ #_ #i #H destruct
]
qed-.

lemma pred_inv_abst: âˆ€M,N. M â¤‡ N â†’ âˆ€A. ğ›Œ.A = M â†’
                     âˆƒâˆƒC. A â¤‡ C & ğ›Œ.C = N.
#M #N * -M -N
[ #i #A0 #H destruct
| #A1 #A2 #HA12 #A0 #H destruct /2 width=3/
| #B1 #B2 #A1 #A2 #_ #_ #A0 #H destruct
| #B1 #B2 #A1 #A2 #_ #_ #A0 #H destruct
]
qed-.

lemma pred_inv_appl: âˆ€M,N. M â¤‡ N â†’ âˆ€B,A. @B.A = M â†’
                     (âˆƒâˆƒD,C. B â¤‡ D & A â¤‡ C & @D.C = N) âˆ¨
                     âˆƒâˆƒA0,D,C0. B â¤‡ D & A0 â¤‡ C0 & ğ›Œ.A0 = A & [â†™D]C0 = N.
#M #N * -M -N
[ #i #B0 #A0 #H destruct
| #A1 #A2 #_ #B0 #A0 #H destruct
| #B1 #B2 #A1 #A2 #HB12 #HA12 #B0 #A0 #H destruct /3 width=5/
| #B1 #B2 #A1 #A2 #HB12 #HA12 #B0 #A0 #H destruct /3 width=7/
]
qed-.

lemma pred_lift: liftable pred.
#h #M1 #M2 #H elim H -M1 -M2 normalize // /2 width=1/
#B1 #B2 #A1 #A2 #_ #_ #IHB12 #IHC12 #d <dsubst_lift_le // /2 width=1/
qed.

lemma pred_inv_lift: deliftable_sn pred.
#h #N1 #N2 #H elim H -N1 -N2 /2 width=3/
[ #C1 #C2 #_ #IHC12 #d #M1 #H
  elim (lift_inv_abst â€¦ H) -H #A1 #HAC1 #H
  elim (IHC12 â€¦ HAC1) -C1 #A2 #HA12 #HAC2 destruct
  @(ex2_intro â€¦ (ğ›Œ.A2)) // /2 width=1/
| #D1 #D2 #C1 #C2 #_ #_ #IHD12 #IHC12 #d #M1 #H
  elim (lift_inv_appl â€¦ H) -H #B1 #A1 #HBD1 #HAC1 #H
  elim (IHD12 â€¦ HBD1) -D1 #B2 #HB12 #HBD2
  elim (IHC12 â€¦ HAC1) -C1 #A2 #HA12 #HAC2 destruct
  @(ex2_intro â€¦ (@B2.A2)) // /2 width=1/
| #D1 #D2 #C1 #C2 #_ #_ #IHD12 #IHC12 #d #M1 #H
  elim (lift_inv_appl â€¦ H) -H #B1 #M #HBD1 #HM #H1
  elim (lift_inv_abst â€¦ HM) -HM #A1 #HAC1 #H
  elim (IHD12 â€¦ HBD1) -D1 #B2 #HB12 #HBD2
  elim (IHC12 â€¦ HAC1) -C1 #A2 #HA12 #HAC2 destruct
  @(ex2_intro â€¦ ([â†™B2]A2)) /2 width=1/
]
qed-.

lemma pred_dsubst: dsubstable pred.
#N1 #N2 #HN12 #M1 #M2 #H elim H -M1 -M2
[ #i #d elim (lt_or_eq_or_gt i d) #Hid
  [ >(dsubst_vref_lt â€¦ Hid) >(dsubst_vref_lt â€¦ Hid) //
  | destruct >dsubst_vref_eq >dsubst_vref_eq /2 width=1/
  | >(dsubst_vref_gt â€¦ Hid) >(dsubst_vref_gt â€¦ Hid) //
  ]
| normalize /2 width=1/
| normalize /2 width=1/
| normalize #B1 #B2 #A1 #A2 #_ #_ #IHB12 #IHC12 #d
  >dsubst_dsubst_ge // /2 width=1/
]
qed.

lemma pred_conf1_vref: âˆ€i. confluent1 â€¦ pred (#i).
#i #M1 #H1 #M2 #H2
<(pred_inv_vref â€¦ H1) -H1 [3: // |2: skip ] (**) (* simplify line *)
<(pred_inv_vref â€¦ H2) -H2 [3: // |2: skip ] (**) (* simplify line *)
/2 width=3/
qed-.

lemma pred_conf1_abst: âˆ€A. confluent1 â€¦ pred A â†’ confluent1 â€¦ pred (ğ›Œ.A).
#A #IH #M1 #H1 #M2 #H2
elim (pred_inv_abst â€¦ H1 â€¦) -H1 [3: // |2: skip ] #A1 #HA1 #H destruct (**) (* simplify line *)
elim (pred_inv_abst â€¦ H2 â€¦) -H2 [3: // |2: skip ] #A2 #HA2 #H destruct (**) (* simplify line *)
elim (IH â€¦ HA1 â€¦ HA2) -A /3 width=3/
qed-.

lemma pred_conf1_appl_beta: âˆ€B,B1,B2,C,C2,M1.
                            (âˆ€M0. |M0| < |B|+|ğ›Œ.C|+1 â†’ confluent1 ? pred M0) â†’ (**) (* ? needed in place of â€¦ *)
                            B â¤‡ B1 â†’ B â¤‡ B2 â†’ ğ›Œ.C â¤‡ M1 â†’ C â¤‡ C2 â†’
                            âˆƒâˆƒM. @B1.M1 â¤‡ M & [â†™B2]C2 â¤‡ M.
#B #B1 #B2 #C #C2 #M1 #IH #HB1 #HB2 #H1 #HC2
elim (pred_inv_abst â€¦ H1 â€¦) -H1 [3: // |2: skip ] #C1 #HC1 #H destruct (**) (* simplify line *)
elim (IH B â€¦ HB1 â€¦ HB2) -HB1 -HB2 //
elim (IH C â€¦ HC1 â€¦ HC2) normalize // -B -C /3 width=5/
qed-.

theorem pred_conf: confluent â€¦ pred.
#M @(f_ind â€¦ size â€¦ M) -M #n #IH * normalize
[ /2 width=3 by pred_conf1_vref/
| /3 width=4 by pred_conf1_abst/
| #B #A #H #M1 #H1 #M2 #H2 destruct
  elim (pred_inv_appl â€¦ H1 â€¦) -H1 [5: // |2,3: skip ] * (**) (* simplify line *)
  elim (pred_inv_appl â€¦ H2 â€¦) -H2 [5,10: // |2,3,7,8: skip ] * (**) (* simplify line *) 
  [ #B2 #A2 #HB2 #HA2 #H2 #B1 #A1 #HB1 #HA1 #H1 destruct
    elim (IH A â€¦ HA1 â€¦ HA2) -HA1 -HA2 //
    elim (IH B â€¦ HB1 â€¦ HB2) // -A -B /3 width=5/
  | #C #B2 #C2 #HB2 #HC2 #H2 #HM2 #B1 #N #HB1 #H #HM1 destruct
    @(pred_conf1_appl_beta â€¦ IH) // (**) (* /2 width=7 by pred_conf1_appl_beta/ does not work *)
  | #B2 #N #B2 #H #HM2 #C #B1 #C1 #HB1 #HC1 #H1 #HM1 destruct
    @ex2_commute @(pred_conf1_appl_beta â€¦ IH) //
  | #C #B2 #C2 #HB2 #HC2 #H2 #HM2 #C0 #B1 #C1 #HB1 #HC1 #H1 #HM1 destruct
    elim (IH B â€¦ HB1 â€¦ HB2) -HB1 -HB2 //
    elim (IH C â€¦ HC1 â€¦ HC2) normalize // -B -C /3 width=5/
  ]
]
qed-.

lemma sred_pred: âˆ€M,N. M â†¦ N â†’ M â¤‡ N.
#M #N #H elim H -M -N /2 width=1/
qed.
