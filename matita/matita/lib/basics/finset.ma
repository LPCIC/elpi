(*
    ||M||  This file is part of HELM, an Hypertextual, Electronic        
    ||A||  Library of Mathematics, developed at the Computer Science     
    ||T||  Department of the University of Bologna, Italy.                     
    ||I||                                                                 
    ||T||  
    ||A||  
    \   /  This file is distributed under the terms of the       
     \ /   GNU General Public License Version 2   
      V_______________________________________________________________ *)

include "basics/lists/listb.ma".

(****** DeqSet: a set with a decidable equality ******)

record FinSet : Type[1] â‰ 
{ FinSetcarr:> DeqSet;
  enum: list FinSetcarr;
  enum_unique: uniqueb FinSetcarr enum = true;
  enum_complete : âˆ€x:FinSetcarr. memb ? x enum = true
}.

notation < "ğ…" non associative with precedence 90 
 for @{'bigF}.
interpretation "FinSet" 'bigF = (mk_FinSet ???).

(* bool *)
lemma bool_enum_unique: uniqueb ? [true;false] = true.
// qed.

lemma bool_enum_complete: âˆ€x:bool. memb ? x [true;false] = true.
* // qed.

definition FinBool â‰ 
  mk_FinSet DeqBool [true;false] bool_enum_unique bool_enum_complete.

unification hint  0 â‰” ; 
    X â‰Ÿ FinBool
(* ---------------------------------------- *) âŠ¢ 
    bool â‰¡ FinSetcarr X.

(* nat segment *)

lemma eqbnat_true : âˆ€n,m. eqb n m = true â†” n = m.
#n #m % [@eqb_true_to_eq | @eq_to_eqb_true]
qed.

definition DeqNat â‰ mk_DeqSet nat eqb eqbnat_true.

lemma lt_to_le : âˆ€n,m. n < m â†’ n â‰¤ m.
/2/ qed-.
 
let rec enumnaux n m â‰ 
  match n return (Î»n.n â‰¤ m â†’ list (Î£x.x < m)) with 
    [ O â‡’ Î»h.[ ] | S p â‡’ Î»h:p < m.(mk_Sig ?? p h)::enumnaux p m (lt_to_le p m h)].
    
definition enumn â‰ Î»n.enumnaux n n (le_n n).

definition Nat_to â‰ Î»n. DeqSig DeqNat (Î»i.i<n).

(* lemma prova : âˆ€n. carr (Nat_to n) = (Î£x.x<n). // *)

lemma memb_enumn: âˆ€m,n,i:DeqNat. âˆ€h:n â‰¤ m. âˆ€k: i < m. n â‰¤ i â†’
  (Â¬ (memb (Nat_to m) (mk_Sig ?? i k) (enumnaux n m h))) = true.
#m #n elim n -n // #n #Hind #i #ltm #k #ltni @sym_eq @noteq_to_eqnot @sym_not_eq
% #H cases (orb_true_l â€¦ H)
  [whd in âŠ¢ (??%?â†’?); #H1 @(absurd  â€¦ ltni) @le_to_not_lt 
   >(eqb_true_to_eq â€¦ H1) @le_n
  |<(notb_notb (memb â€¦)) >Hind normalize /2 by lt_to_le, absurd/
  ]
qed. 

lemma enumn_unique_aux: âˆ€n,m. âˆ€h:n â‰¤ m. uniqueb (Nat_to m) (enumnaux n m h) = true.
#n elim n -n // #n #Hind #m #h @true_to_andb_true // @memb_enumn //
qed.
 
lemma enumn_unique: âˆ€n.uniqueb (Nat_to n) (enumn n) = true.
#n @enumn_unique_aux
qed.

(* definition ltb â‰ Î»n,m.leb (S n) m. *)
lemma enumn_complete_aux: âˆ€n,m,i.âˆ€h:n â‰¤m.âˆ€k:i<m.i<n â†’ 
  memb (Nat_to m) (mk_Sig ?? i k) (enumnaux n m h) = true.
#n elim n -n
  [normalize #n #i #_ #_ #Hfalse @False_ind /2/ 
  |#n #Hind #m #i #h #k #lein whd in âŠ¢ (??%?);
   cases (le_to_or_lt_eq â€¦ (le_S_S_to_le â€¦ lein))
    [#ltin cut (eqb (Nat_to m) (mk_Sig ?? i k) (mk_Sig ?? n h) = false)
      [normalize @not_eq_to_eqb_false @lt_to_not_eq @ltin] 
       #Hcut >Hcut @Hind //
    |#eqin cut (eqb (Nat_to m) (mk_Sig ?? i k) (mk_Sig ?? n h) = true)
     [normalize @eq_to_eqb_true //
     |#Hcut >Hcut %
    ]
  ]
qed.

lemma enumn_complete: âˆ€n.âˆ€i:Nat_to n. memb ? i (enumn n) = true.
#n whd in âŠ¢ (%â†’?); * #i #ltin @enumn_complete_aux //
qed.

definition initN â‰ Î»n.
  mk_FinSet (Nat_to n) (enumn n) (enumn_unique n) (enumn_complete n).

example tipa: âˆ€n.âˆƒx: initN (S n). pi1 â€¦ x = n.
#n @ex_intro [whd @mk_Sig [@n | @le_n] | //] qed.

(* option *)
definition enum_option â‰ Î»A:DeqSet.Î»l.
  None A::(map ?? (Some A) l).
  
lemma enum_option_def : âˆ€A:FinSet.âˆ€l. 
  enum_option A l = None A :: (map ?? (Some A) l).
// qed.

lemma enum_option_unique: âˆ€A:DeqSet.âˆ€l. 
  uniqueb A l = true â†’ 
    uniqueb ? (enum_option A l) = true.
#A #l #uA @true_to_andb_true
  [generalize in match uA; -uA #_ elim l [%]
   #a #tl #Hind @sym_eq @noteq_to_eqnot % #H 
   cases (orb_true_l â€¦ (sym_eq â€¦ H))
    [#H1 @(absurd (None A = Some A a)) [@(\P H1) | % #H2 destruct]
    |-H #H >H in Hind; normalize /2/
    ]
  |@unique_map_inj // #a #a1 #H destruct %
  ]
qed.

lemma enum_option_complete: âˆ€A:DeqSet.âˆ€l. 
  (âˆ€x:A. memb A x l = true) â†’
    âˆ€x:DeqOption A. memb ? x (enum_option A l) = true.
#A #l #Hl * // #a @memb_cons @memb_map @Hl
qed.
    
definition FinOption â‰ Î»A:FinSet.
  mk_FinSet (DeqOption A) 
   (enum_option A (enum A)) 
   (enum_option_unique â€¦ (enum_unique A))
   (enum_option_complete â€¦ (enum_complete A)).

unification hint  0 â‰” C; 
    T â‰Ÿ FinSetcarr C,
    X â‰Ÿ FinOption C
(* ---------------------------------------- *) âŠ¢ 
    option T â‰¡ FinSetcarr X.

(* sum *)
definition enum_sum â‰ Î»A,B:DeqSet.Î»l1.Î»l2.
  (map ?? (inl A B) l1) @ (map ?? (inr A B) l2).
  
lemma enum_sum_def : âˆ€A,B:FinSet.âˆ€l1,l2. enum_sum A B l1 l2 = 
  (map ?? (inl A B) l1) @ (map ?? (inr A B) l2).
// qed.

lemma enum_sum_unique: âˆ€A,B:DeqSet.âˆ€l1,l2. 
  uniqueb A l1 = true â†’ uniqueb B l2 = true â†’ 
    uniqueb ? (enum_sum A B l1 l2) = true.
#A #B #l1 #l2 elim l1 
  [#_ #ul2 @unique_map_inj // #b1 #b2 #Hinr destruct //
  |#a #tl #Hind #uA #uB @true_to_andb_true 
    [@sym_eq @noteq_to_eqnot % #H 
     cases (memb_append â€¦ (sym_eq â€¦ H))
      [#H1 @(absurd (memb ? a tl = true)) 
        [@(memb_map_inj â€¦H1) #a1 #a2 #Hinl destruct //
        |<(andb_true_l â€¦ uA) @eqnot_to_noteq //
        ]
      |elim l2
        [normalize #H destruct 
        |#b #tlB #Hind #membH cases (orb_true_l â€¦ membH)
          [#H lapply (\P H) #H1 destruct |@Hind]
        ]
      ] 
    |@Hind // @(andb_true_r â€¦ uA)
    ]
  ]
qed.

lemma enum_sum_complete: âˆ€A,B:DeqSet.âˆ€l1,l2. 
  (âˆ€x:A. memb A x l1 = true) â†’
  (âˆ€x:B. memb B x l2 = true) â†’
    âˆ€x:DeqSum A B. memb ? x (enum_sum A B l1 l2) = true.
#A #B #l1 #l2 #Hl1 #Hl2 *
  [#a @memb_append_l1 @memb_map @Hl1
  |#b @memb_append_l2 @memb_map @Hl2
  ]
qed.
    
definition FinSum â‰ Î»A,B:FinSet.
  mk_FinSet (DeqSum A B) 
   (enum_sum A B (enum A) (enum B)) 
   (enum_sum_unique â€¦ (enum_unique A) (enum_unique B))
   (enum_sum_complete â€¦ (enum_complete A) (enum_complete B)).

include alias "basics/types.ma".

unification hint  0 â‰” C1,C2; 
    T1 â‰Ÿ FinSetcarr C1,
    T2 â‰Ÿ FinSetcarr C2,
    X â‰Ÿ FinSum C1 C2
(* ---------------------------------------- *) âŠ¢ 
    T1+T2 â‰¡ FinSetcarr X.

(* prod *)

definition enum_prod â‰ Î»A,B:DeqSet.Î»l1.Î»l2.
  compose ??? (mk_Prod A B) l1 l2.

lemma enum_prod_unique: âˆ€A,B,l1,l2. 
  uniqueb A l1 = true â†’ uniqueb B l2 = true â†’
  uniqueb ? (enum_prod A B l1 l2) = true.
#A #B #l1 elim l1 //
  #a #tl #Hind #l2 #H1 #H2 @uniqueb_append 
  [@unique_map_inj [#x #y #Heq @(eq_f â€¦ \snd â€¦ Heq) | //]
  |@Hind // @(andb_true_r â€¦ H1)
  |#p #H3 cases (memb_map_to_exists â€¦ H3) #b * 
   #Hmemb #eqp <eqp @(not_to_not ? (memb ? a tl = true))
    [2: @sym_not_eq @eqnot_to_noteq @sym_eq @(andb_true_l â€¦ H1)
    |elim tl 
      [normalize //
      |#a1 #tl1 #Hind2 #H4 cases (memb_append â€¦ H4) -H4 #H4
        [cases (memb_map_to_exists â€¦ H4) #b1 * #memb1 #H destruct (H)
         normalize >(\b (refl ? a)) //
        |@orb_true_r2 @Hind2 @H4
        ]
      ]
    ]
  ]
qed.

lemma enum_prod_complete:âˆ€A,B:DeqSet.âˆ€l1,l2.
  (âˆ€a. memb A a l1 = true) â†’ (âˆ€b.memb B b l2 = true) â†’
    âˆ€p. memb ? p (enum_prod A B l1 l2) = true.
#A #B #l1 #l2 #Hl1 #Hl2 * #a #b @memb_compose // 
qed.

definition FinProd â‰ 
Î»A,B:FinSet.mk_FinSet (DeqProd A B)
  (enum_prod A B (enum A) (enum B)) 
  (enum_prod_unique A B â€¦ (enum_unique A) (enum_unique B)) 
  (enum_prod_complete A B â€¦ (enum_complete A) (enum_complete B)).

unification hint  0 â‰” C1,C2; 
    T1 â‰Ÿ FinSetcarr C1,
    T2 â‰Ÿ FinSetcarr C2,
    X â‰Ÿ FinProd C1 C2
(* ---------------------------------------- *) âŠ¢ 
    T1Ã—T2 â‰¡ FinSetcarr X.

(* graph of a function *)

definition graph_of â‰ Î»A,B.Î»f:Aâ†’B. 
  Î£p:AÃ—B.f (\fst p) = \snd p.

definition graph_enum â‰ Î»A,B:FinSet.Î»f:Aâ†’B. 
  filter ? (Î»p.f (\fst p) == \snd p) (enum (FinProd A B)).
  
lemma graph_enum_unique : âˆ€A,B,f.
  uniqueb ? (graph_enum A B f) = true.
#A #B #f @uniqueb_filter @(enum_unique (FinProd A B))
qed.

lemma graph_enum_correct: âˆ€A,B:FinSet.âˆ€f:Aâ†’B. âˆ€a,b.
memb ? âŒ©a,bâŒª (graph_enum A B f) = true â†’ f a = b.
#A #B #f #a #b #membp @(\P ?) @(filter_true â€¦ membp)
qed.

lemma graph_enum_complete: âˆ€A,B:FinSet.âˆ€f:Aâ†’B. âˆ€a,b.
f a = b â†’ memb ? âŒ©a,bâŒª (graph_enum A B f) = true.
#A #B #f #a #b #eqf @memb_filter_l [@(\b eqf)]
@enum_prod_complete //
qed.
